{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "7cb88d52-ef82-4eab-928b-42665f9ce324",
   "metadata": {},
   "source": [
    "# Main simulation in Julia"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "119432ac-533a-4a8b-9b07-8019edf792d0",
   "metadata": {},
   "outputs": [],
   "source": [
    "using CUDA\n",
    "using MAT\n",
    "using LinearAlgebra\n",
    "using Random\n",
    "using Statistics\n",
    "using NPZ\n",
    "using Printf\n",
    "\n",
    "# Simulation parameters\n",
    "g_to_kg = 1e-3\n",
    "kgm3_to_ppb = 1.529e7\n",
    "\n",
    "u      = 2.5\n",
    "Ry, ry = 0.306, 0.885\n",
    "Rz, rz = 0.072, 1.021\n",
    "\n",
    "domain_size = 40000\n",
    "resolution = 1\n",
    "nx = domain_size ÷ resolution\n",
    "ny = nx\n",
    "\n",
    "subgrid_size_m = 4000\n",
    "subgrid_size_px = subgrid_size_m ÷ resolution\n",
    "n_subgrid = domain_size ÷ subgrid_size_m\n",
    "\n",
    "# ==== CUDA Kernel ====\n",
    "function kernel_total_conc_add!(\n",
    "    C, nx, ny, dx, dy,\n",
    "    sources_x, sources_y, sources_q,\n",
    "    u, Ry, ry, Rz, rz\n",
    ")\n",
    "    ix = (blockIdx().x - 1) * blockDim().x + threadIdx().x\n",
    "    iy = (blockIdx().y - 1) * blockDim().y + threadIdx().y\n",
    "    if ix > nx || iy > ny\n",
    "        return\n",
    "    end\n",
    "\n",
    "    xpos = (ix - 1) * dx\n",
    "    ypos = (iy - 1) * dy\n",
    "\n",
    "    c = 0.0\n",
    "    ns = length(sources_x)\n",
    "    @inbounds for k in 1:ns\n",
    "        sx = sources_x[k]\n",
    "        sy = sources_y[k]\n",
    "        q  = sources_q[k]\n",
    "\n",
    "        δy = sy - ypos\n",
    "        if δy <= 0.0\n",
    "            continue\n",
    "        end\n",
    "\n",
    "        σy = Ry * δy^ry\n",
    "        σz = Rz * δy^rz\n",
    "\n",
    "        term1 = q / (π * u * σy * σz)\n",
    "        term2 = exp(-((xpos - sx)^2) / (2.0 * σy^2))\n",
    "        c += term1 * term2\n",
    "    end\n",
    "\n",
    "    C[ix, iy] += c\n",
    "end\n",
    "\n",
    "# build source lists from source map\n",
    "function build_sources(source_map::Matrix{Float64}; offset_x=0.0, offset_y=0.0)\n",
    "    sx_list = Float64[]\n",
    "    sy_list = Float64[]\n",
    "    sq_list = Float64[]\n",
    "\n",
    "    @inbounds for j in 1:size(source_map, 2), i in 1:size(source_map, 1)\n",
    "        q_gps = source_map[i, j]\n",
    "        if q_gps == 0.0\n",
    "            continue\n",
    "        end\n",
    "        q = q_gps * g_to_kg  # g/s → kg/s\n",
    "        push!(sx_list, (i - 1 + rand()) + offset_x)\n",
    "        push!(sy_list, (j - 1 + rand()) + offset_y)\n",
    "        push!(sq_list, q)\n",
    "    end\n",
    "\n",
    "    return (sx_list, sy_list, sq_list)\n",
    "end\n",
    "\n",
    "function simulate_and_save(C::CuArray{Float64}, sx, sy, sq, res, fname)\n",
    "    threads = (16, 16)\n",
    "    blocks = (cld(size(C,1), threads[1]), cld(size(C,2), threads[2]))\n",
    "    batch_size = 1_000_000\n",
    "    total_sources = length(sx)\n",
    "    total_batches = ceil(Int, total_sources / batch_size)\n",
    "\n",
    "    for batch in 1:total_batches\n",
    "        range = (batch-1)*batch_size+1 : min(batch*batch_size, total_sources)\n",
    "        sources_x = CuArray(sx[range])\n",
    "        sources_y = CuArray(sy[range])\n",
    "        sources_q = CuArray(sq[range])\n",
    "\n",
    "        @cuda threads=threads blocks=blocks kernel_total_conc_add!(\n",
    "            C, size(C,1), size(C,2), res, res,\n",
    "            sources_x, sources_y, sources_q,\n",
    "            u, Ry, ry, Rz, rz\n",
    "        )\n",
    "        synchronize()\n",
    "    end\n",
    "\n",
    "    C_cpu = Array(C)\n",
    "    C_ppb = C_cpu .* kgm3_to_ppb\n",
    "\n",
    "    matopen(fname, \"w\") do f\n",
    "        write(f, \"C\", C_ppb)\n",
    "    end\n",
    "end\n",
    "\n",
    "function run_simulation(input_path::String)\n",
    "    println(\"Load the source map: $input_path\")\n",
    "    full_map = convert(Matrix{Float64}, npzread(input_path))\n",
    "    @assert size(full_map) == (nx, ny) \"The size of source map should be $(nx)x$(ny)\"\n",
    "    \n",
    "    println(\"Starting full domain simulation.\")\n",
    "    sx, sy, sq = build_sources(full_map)\n",
    "    C_full = CUDA.zeros(Float64, nx, ny)\n",
    "    simulate_and_save(C_full, sx, sy, sq, resolution,\n",
    "        joinpath(output_dir, replace(basename(input_path), \".npy\" => \"_fullsim.mat\")))\n",
    "    println(\"Full simulation complete.\")\n",
    "\n",
    "    println(\"Starting local block simulations.\")\n",
    "    for i in 0:n_subgrid-1, j in 0:n_subgrid-1\n",
    "        x_start = i * subgrid_size_px + 1\n",
    "        x_end   = (i+1) * subgrid_size_px\n",
    "        y_start = j * subgrid_size_px + 1\n",
    "        y_end   = (j+1) * subgrid_size_px\n",
    "\n",
    "        submap = full_map[x_start:x_end, y_start:y_end]\n",
    "        if all(submap .== 0f0)\n",
    "            continue\n",
    "        end\n",
    "\n",
    "        println(\"Non-zero block: ($i, $j), running local simulation.\")\n",
    "        offset_x = x_start - 1\n",
    "        offset_y = y_start - 1\n",
    "        sx, sy, sq = build_sources(submap; offset_x=offset_x, offset_y=offset_y)\n",
    "        \n",
    "        C_local = CUDA.zeros(Float64, subgrid_size_px, subgrid_size_px)\n",
    "        output_name = joinpath(output_dir, @sprintf(\"%s_block_x%02d_y%02d.mat\",\n",
    "                replace(basename(input_path), \".npy\" => \"\"), i, j))\n",
    "        simulate_and_save(C_local, sx, sy, sq, resolution, output_name)\n",
    "    end\n",
    "\n",
    "    println(\"Simulation complete.\")\n",
    "    \n",
    "end\n",
    "\n",
    "run_simulation(\"/output_sources/400m.npy\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
